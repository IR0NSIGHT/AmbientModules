/* 
*	@Author: IR0NSIGHT 
*	@Date: 2022-06-03 17:38:32 
*	@Last Modified time: 2022-06-03 17:38:32 
*	 
*	Description: 
*		opens a ZEN dialog (GUI) that allows to specifiy an artillery barrage at the defined position
* 
*	Environment: 
*		CLIENT, SUSPENDABLE 
*		 
*	Parameter(s): 
*		0: posASL - position of artillery barrage 
*		 
*	Returns: 
*		none
* 
*	Examples: 
*		[getPosWorld player] call IRN_fnc_artilleryVolleyDialog
*/

params ["_pos"];
//See ZEN framework for documentation https://zen-mod.github.io/ZEN/#/frameworks/dynamic_dialog

_shapeSelection = [
	"COMBO",
	["Shape type","Which shape the bombarded area will have"],
	[["circle","rect"],["Circle","Rectangle"],0]
];

_radiusC = [
	"slider",
	"Circle radius",
	[25,1000,100,0],//
	false
];

//support for RECT shape
_rectDimensionsC = [
	"VECTOR",
	["rectangle dimensions","vector that describes the dimensions of the reactuangulare area that will be bombarded."],
	[200,100]
];

//creeping barrage 
_movingDistance = [
	"COMBO",
	["Moving distance","Barrage will move forward this distance over its lifetime"],
	[[0,100,250,500],["0m","100m","250m","500m"],0],
	false
];

_durationC = [
	"slider",
	"Duration (seconds)",
	[3,300,15,0],//
	false
];

_delay = [
	"COMBO",
	"Delay",
	[
		[0,10,30,60,120,300],
		["0 sec","10 sec","30 sec","60 sec","2 min","5 min"],
		0
	],
	false
];

_discreteValues = [0.1,0.2,0.5,1,2,4,8,16,32];
_intensityC = [
	"COMBO",
	["Seconds between impacts","Average time between to shell impacts"],
	[
		_discreteValues,
		_discreteValues apply {(str _x)+" sec"},
		4
	],
	false
];
_projectiles = [
	//category: flare, smoke, HE
	//format: [classname, displayname, isFlare]
	//40mm UGL
	["F_40mm_White"," 40mm Flare white",true],
	["F_40mm_Red"," 40mm Flare red",true],
	["F_40mm_Green"," 40mm Flare green",true],
	["G_40mm_HE"," 40mm HE",false],

	//82mm mortar
//	["Flare_82mm_AMOS_White"," 82mm Flare",true],	//TODO doesnt work?
	["Smoke_82mm_AMOS_White"," 82mm Smoke",false],
	["Sh_82mm_AMOS"," 82mm HE",false],

	//155mm mortar
	["Smoke_120mm_AMOS_White","155mm Smoke ",false],
	["Sh_155mm_AMOS","155mm HE",false],

	//230mm MRL rocket
	["R_230mm_HE","230mm HE Rocket",false]
];


_projectileC = [
		"COMBO",
		"Mortar ammo type",
		[
			_projectiles apply {[_x#0,_x#2]},
			_projectiles apply {_x#1},
			0
		],
		false
];

_title = "Artillery Barrage";
_content = [
	_shapeSelection,
	_radiusC,
	_rectDimensionsC,
	_durationC,
	_intensityC,
	_projectileC,
	_delay,
	_movingDistance
];


_onConfirm = {	//LOCAL TO ZEUS PLAYER
	params["_output","_args"];
	//parse parameters for artillery function
	_output params ["_shapeType","_radius","_rectDims","_duration","_impactOffset","_projectile","_delay","_movingDist"];
	_projectile params ["_projClass","_isFlare"];
	_spawnHeight = 300;
	_speed = 200;
	if (_isFlare) then {
		_spawnHeight = 100;
		_speed = 0.5;
	};
	_args params ["_pos"];
	//--

	//spawn anchor object
	_anchor = createVehicle ["Sign_Pointer_Blue_F",_pos];	//anchor is global object, but local to zeus
	_anchor hideObjectGlobal true;
	_anchor setPosASL _pos;
	{
		[_x,[[_anchor], true]] remoteExec ["addCuratorEditableObjects",2,false];
	} forEach allCurators;

	//initialize shape
	_shape = [_shapeType,_anchor,[]];
	if (_shapeType isEqualTo "circle") then {
		_shape set [2,[_radius]]; 
	} else {
		_shape set [2,[[_rectDims#0,0,0],[0,_rectDims#1,0]]]; 
	};
	_validShape = [_shape] call irn_fnc_isShape;
	if (!_validShape) exitWith {
		["invalid shape generated by artillery volley dialog"] call BIS_Fnc_error; 
	};
	_outlineObjs = [_anchor,_shape,25,true,true] call IRN_fnc_outlineShape;


	//align anchor to up vector
	[_anchor] spawn {
		params ["_anchor"];
		while {!isNull _anchor} do {
			_anchor setVectorUp [0,0,1];
			sleep 0.2;
		};
	};

	//call artillery volley function on server
	_args = [_shape, _duration, _impactOffset,  _projClass, _speed,_spawnHeight];
	[_delay,_args] spawn {
		params ["_delay","_args"];
		sleep _delay;
		diag_log ["calling mortar fire with args:",_args];
		//_args execVM "fn_artilleryVolley.sqf";
		_args remoteExec ["IRN_fnc_artilleryVolley",2];
	};

	//move anchor+shape for rolling barrage
	[_delay,_duration,_anchor,_outlineObjs,_movingDist] spawn {
		params ["_delay","_duration","_anchor","_objs","_movingDist"];
		sleep _delay;
		for "_i" from 0 to _duration do {
			_step = (vectorDir _anchor) vectorMultiply (_movingDist/_duration);
			_anchor setPosWorld (getPosWorld _anchor vectorAdd _step);
			sleep 1;
		};
		deleteVehicle _anchor;
	};
};

_onCancel = {};
_args = [_pos];

[_title,_content,_onConfirm,_onCancel,_args] call zen_dialog_fnc_create;


